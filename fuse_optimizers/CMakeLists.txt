cmake_minimum_required(VERSION 3.16)
project(fuse_optimizers)

# Default to C++17
if(NOT CMAKE_CXX_STANDARD)
  set(CMAKE_CXX_STANDARD 17)
  set(CXX_STANDARD_REQUIRED YES)
endif()

if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
  add_compile_options(-Wall -Wextra -Wpedantic)
endif()

find_package(ament_cmake_ros REQUIRED)
find_package(rclcpp REQUIRED)

find_package(fuse_constraints REQUIRED)
find_package(fuse_core REQUIRED)
find_package(fuse_msgs REQUIRED)
find_package(fuse_graphs REQUIRED)
find_package(fuse_variables REQUIRED)

find_package(std_srvs REQUIRED)
find_package(diagnostic_updater REQUIRED)
find_package(pluginlib REQUIRED)
find_package(rclcpp_components REQUIRED)

find_package(Ceres REQUIRED)
find_package(Eigen3 REQUIRED)

###########
## Build ##
###########

## fuse_optimizers library
add_library(${PROJECT_NAME}
  src/batch_optimizer.cpp
  src/fixed_lag_smoother.cpp
  src/optimizer.cpp
  src/variable_stamp_index.cpp
)
target_include_directories(${PROJECT_NAME} PUBLIC
  "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>"
  "$<INSTALL_INTERFACE:include/${PROJECT_NAME}>"
)
target_link_libraries(${PROJECT_NAME}
  fuse_constraints::fuse_constraints
  fuse_core::fuse_core
  fuse_graphs::fuse_graphs
  ${fuse_msgs_TARGETS}
  fuse_variables::fuse_variables
  pluginlib::pluginlib
  ${std_srvs_TARGETS}
)
ament_target_dependencies(${PROJECT_NAME} diagnostic_updater)

## batch_optimizer component
add_library(batch_optimizer_component SHARED src/batch_optimizer_component.cpp)
target_include_directories(batch_optimizer_component PRIVATE include)
ament_target_dependencies(
  batch_optimizer_component
  SYSTEM
  rclcpp_components
)
target_link_libraries(batch_optimizer_component ${PROJECT_NAME})

rclcpp_components_register_node(
  batch_optimizer_component PLUGIN "fuse_optimizers::BatchOptimizerComponent" EXECUTABLE
  batch_optimizer_node)

## fixed_lag_smoother component
add_library(fixed_lag_smoother_component SHARED src/fixed_lag_smoother_component.cpp)
target_include_directories(fixed_lag_smoother_component PRIVATE include)
ament_target_dependencies(
  fixed_lag_smoother_component
  SYSTEM
  rclcpp_components
)
target_link_libraries(fixed_lag_smoother_component ${PROJECT_NAME})

rclcpp_components_register_node(
  fixed_lag_smoother_component PLUGIN "fuse_optimizers::FixedLagSmootherComponent" EXECUTABLE
  fixed_lag_smoother_node)

#############
## Testing ##
#############

if(BUILD_TESTING)
  find_package(ament_lint_auto REQUIRED)
  ament_lint_auto_find_test_dependencies()
  add_subdirectory(test)
endif()

#############
## Install ##
#############

install(TARGETS ${PROJECT_NAME} batch_optimizer_component fixed_lag_smoother_component EXPORT ${PROJECT_NAME}-export
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  RUNTIME DESTINATION bin
)

install(TARGETS
  batch_optimizer_node
  fixed_lag_smoother_node
  DESTINATION lib/${PROJECT_NAME}
)

install(DIRECTORY include/
  DESTINATION include/${PROJECT_NAME}
)

ament_export_targets(${PROJECT_NAME}-export HAS_LIBRARY_TARGET)
ament_export_dependencies(
  ament_cmake_ros
  rclcpp
  fuse_constraints
  fuse_core
  fuse_msgs
  fuse_graphs
  fuse_variables
  std_srvs
  diagnostic_updater
  pluginlib
  rclcpp_components

  Ceres
  Eigen3
)

ament_package()
